apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.7"
}
//这个就是具体解析ec文件的任务，会根据我们指定的class路径、源码路径、ec路径进行解析输出
def fileFilter = [
        '**/databinding/*Binding.*',
        '**/databinding/*BindingImpl.*',
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*',
        // kotlin
        '**/*MapperImpl*.*',
        '**/DataBindingTriggerClass.*',
        '**/*$ViewInjector*.*',
        '**/*$ViewBinder*.*',
        '**/BuildConfig.*',
        '**/*Component*.*',
        '**/*BR*.*',
        '**/Manifest*.*',
        '**/*$Lambda$*.*',
        '**/*Companion*.*',
        '**/*_Factory*.*',
        '**/*_Provide*Factory*.*',
        '**/*Extensions*.*'
]
def coverageSourceDirs = [
        "${rootDir}/app/src/main/java"
]
def javaClassDirPath = ["$buildDir/intermediates/javac/debug"]
def kotlinClassDirPath = ["$buildDir/tmp/kotlin-classes/debug"]
def coverageClassDirPaths = javaClassDirPath + kotlinClassDirPath
def coverageClassFileTrees =
        coverageClassDirPaths.collect {
            fileTree(dir: it, excludes: fileFilter)
        }

task jacocoTestReport(type: JacocoReport) {
    group = "JacocoReport"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }

    sourceDirectories.setFrom(files(coverageSourceDirs))
    classDirectories.setFrom(coverageClassFileTrees)
    executionData.setFrom(files("$rootDir/build/aa.ec"))
    doFirst {
        //遍历class路径下的所有文件，替换字符
        coverageClassDirPaths.each { path ->
            new File(path).eachFileRecurse { file ->
                if (file.name.contains('$$')) {
                    file.renameTo(file.path.replace('$$', '$'))
                }
            }
        }
    }

}
